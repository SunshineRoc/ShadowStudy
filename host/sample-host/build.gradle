//apply plugin: 'com.android.application'
apply plugin: 'com.android.library'

android {
    compileSdkVersion project.COMPILE_SDK_VERSION
    defaultConfig {
//        applicationId project.SAMPLE_HOST_APP_APPLICATION_ID
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
        testInstrumentationRunner "com.tencent.shadow.test.CustomAndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }
    sourceSets {
        debug {
            assets.srcDir('build/generated/assets/sample-manager/debug/')
            assets.srcDir('build/generated/assets/plugin-zip/debug/')
        }
        release {
            assets.srcDir('build/generated/assets/sample-manager/release/')
            assets.srcDir('build/generated/assets/plugin-zip/release/')
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "commons-io:commons-io:$commons_io_android_version"//sample-host从assets中复制插件用的
    implementation "org.slf4j:slf4j-api:$slf4j_version"

    implementation 'com.tencent.shadow.core:common'
    implementation 'com.tencent.shadow.dynamic:dynamic-host'
    implementation project(':sample-constant')
    implementation project(':sample-host-lib')
}

/**
 * 复制文件的任务
 *
 * @param projectName: 模块名
 * @param buildType：  打包类型：debug或release
 * @param name：       复制到的目标文件的目录名
 * @param apkName：    复制到的目标文件的文件名
 * @param inputFile：  待复制的原文件
 * @param taskName：   任务名
 *
 * */
def createCopyTask(projectName, buildType, name, apkName, inputFile, taskName) {
    def outputFile = file("${getBuildDir()}/generated/assets/${name}/${buildType}/${apkName}")
    outputFile.getParentFile().mkdirs()
    return tasks.create("copy${buildType.capitalize()}${name.capitalize()}${apkName.capitalize()}Task", Copy) {
        group = 'build'
        description = "复制${name}到assets中."
        from(inputFile.getParent()) {
            include(inputFile.name)
            rename { outputFile.name }
        }
        into(outputFile.getParent())

    }.dependsOn("${projectName}:${taskName}")
}

def generateAssets(generateAssetsTask, buildType) {

    // 把PluginManager打包为宿主 build 的 assets 目录中的plugin-manager.apk
    def moduleName = 'sample-manager'
    def pluginManagerApkFile = file(
            "${project(":sample-manager").getBuildDir()}" +
                    "/outputs/apk/${buildType}/" +
                    "${moduleName}-${buildType}.apk"
    )
    generateAssetsTask.dependsOn createCopyTask(
            ':sample-manager',
            buildType,
            moduleName,
            'plugin-manager.apk',
            pluginManagerApkFile,
            "assemble${buildType.capitalize()}"
    )

    // 插件zip包的原文件
    def pluginZip = file("${getRootProject().getBuildDir()}/plugin-${buildType}.zip")

    // 把插件1打包到宿主 build 的 assets 目录中的plugin1-debug/release.zip文件中
    generateAssetsTask.dependsOn createCopyTask(
            ':sample-app1',                                // 模块名称
            buildType,                                     // 打包类型：debug或release
            'plugin-zip',                                  // zip包所在的目录的名称
            "plugin1-${buildType}.zip",                    // zip包的名称
            pluginZip,                                     // 待复制的原文件
            "package${buildType.capitalize()}Plugin"       // 任务的名称
    )

    // 把插件2打包到宿主 build 的 assets 目录中的plugin2-debug/release.zip文件中
//    generateAssetsTask.dependsOn createCopyTask(
//            ':sample-app2',
//            buildType,
//            'plugin-zip',
//            "plugin2-${buildType}.zip",
//            pluginZip,
//            "package${buildType.capitalize()}Plugin"
//    )

    // 把插件3打包到宿主 build 的 assets 目录中的plugin3-debug/release.zip文件中
//    generateAssetsTask.dependsOn createCopyTask(
//            ':sample-app3',
//            buildType,
//            'plugin-zip',
//            "plugin3-${buildType}.zip",
//            pluginZip,
//            "package${buildType.capitalize()}Plugin"
//    )

    println("generateAssets() ==》 打包：getRootProject().getBuildDir()=${getRootProject().getBuildDir()}" +
            "，buildType=$buildType" +
            "，pluginZip.path=" + pluginZip.path)
}

tasks.whenTaskAdded { task ->
    if (task.name == "generateDebugAssets") {
        generateAssets(task, 'debug')
    }
    if (task.name == "generateReleaseAssets") {
        generateAssets(task, 'release')
    }
}